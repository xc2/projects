load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//watermark:vite/package_json.bzl", vite_bin = "bin")
load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_devserver", "js_test")
load("//tools/wrangler:defs.bzl", "pages_deploy")

npm_link_all_packages(name = "node_modules")

exports_files(
    [".swcrc"],
    visibility = [":__subpackages__"],
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.ts.json",
    visibility = [":__subpackages__"],
)

ts_config(
    name = "vite-tsconfig",
    src = "tsconfig.node.json",
)

# gazelle:js_project_naming_convention bundle-configs
ts_project(
    name = "bundle-configs",
    srcs = [
        "postcss.config.ts",
        "tailwind.config.ts",
        "vite.config.ts",
    ],
    composite = True,
    tsconfig = ":vite-tsconfig",
    deps = [
        ":node_modules/@types/node",
        ":node_modules/@vitejs/plugin-react",
        ":node_modules/tailwindcss",
        ":node_modules/tailwindcss-animate",
        ":node_modules/vite",
        ":node_modules/vite-plugin-pwa",
    ],
)

SRC = [
    ":index.html",
    "//watermark/src",
] + glob(["public/*"])

VITE_DEPS = [
    ":tsconfig",
    "bundle-configs",
    ":node_modules/tailwindcss",
    ":node_modules/postcss",
    ":node_modules/autoprefixer",
]

vite_bin.vite(
    name = "watermark",
    srcs = SRC + VITE_DEPS,
    args = [
        "build",
        "-c",
        "vite.config.js",
        "--outDir",
        "dist",
    ],
    chdir = package_name(),
    out_dirs = ["dist"],
)

vite_bin.vite_binary(
    name = "_vite",
    chdir = package_name(),
)

js_run_devserver(
    name = "dev",
    args = [
        "--host",
        "127.0.0.1",
        "--port",
        "3001",
        "--open",
        "false",
        "-c",
        "vite.config.js",
        "--clearScreen",
        "false",
    ],
    data = SRC + VITE_DEPS,
    tool = ":_vite",
)

pages_deploy(
    src = ":watermark",
    project = "watermark",
)
