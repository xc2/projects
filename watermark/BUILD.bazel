load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//watermark:@rsbuild/core/package_json.bzl", rsbuild_bin = "bin")
load("@aspect_rules_js//js:defs.bzl", "js_run_devserver", "js_test")
load("//tools/wrangler:defs.bzl", "pages_deploy")

npm_link_all_packages(name = "node_modules")

exports_files(
    [".swcrc"],
    visibility = [":__subpackages__"],
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.ts.json",
    visibility = [":__subpackages__"],
)

ts_config(
    name = "node-tsconfig",
    src = "tsconfig.node.json",
)

# gazelle:js_project_naming_convention bundle-configs
ts_project(
    name = "bundle-configs",
    srcs = [
        "postcss.config.ts",
        "rsbuild.config.ts",
        "tailwind.config.ts",
    ],
    composite = True,
    tsconfig = ":node-tsconfig",
    deps = [
        ":node_modules/@rsbuild/core",
        ":node_modules/tailwindcss",
        ":node_modules/tailwindcss-animate",
    ],
)

SRC = [
    ":index.html",
    "//watermark/src",
] + glob(["public/*"])

BUNDLE_DEPS = [
    "bundle-configs",
    ":node_modules/tailwindcss",
    ":node_modules/postcss",
    ":node_modules/autoprefixer",
]

rsbuild_bin.rsbuild(
    name = "watermark",
    srcs = SRC + BUNDLE_DEPS,
    args = [
        "build",
    ],
    chdir = package_name(),
    out_dirs = ["dist"],
)

rsbuild_bin.rsbuild_binary(
    name = "_rsbuild",
    chdir = package_name(),
)

js_run_devserver(
    name = "dev",
    args = [
        "dev",
    ],
    data = SRC + BUNDLE_DEPS,
    tool = ":_rsbuild",
)

pages_deploy(
    src = ":watermark",
    project = "watermark",
)
